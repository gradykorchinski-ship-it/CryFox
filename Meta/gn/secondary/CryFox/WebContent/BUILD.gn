import("//CryFox/enable_qt.gni")
import("//CryFox/link_qt.gni")
import("//CryFox/moc_qt_objects.gni")
import("//Meta/gn/build/libs/pulse/enable.gni")

moc_qt_objects("generate_moc") {
  sources = [ "//CryFox/Qt/EventLoopImplementationQtEventTarget.h" ]
}

link_qt("WebContent_qt") {
  qt_components = [ "Core" ]
}

executable("WebContent") {
  configs += [ "//CryFox:cryfox_config" ]
  include_dirs = [
    "//Userland/Services",
    "//CryFox",
  ]
  deps = [
    "//AK",
    "//Meta/gn/build/libs/fontconfig",
    "//Meta/gn/build/libs/pulse",
    "//Userland/Libraries/LibCore",
    "//Userland/Libraries/LibFileSystem",
    "//Userland/Libraries/LibGfx",
    "//Userland/Libraries/LibIPC",
    "//Userland/Libraries/LibImageDecoderClient",
    "//Userland/Libraries/LibJS",
    "//Userland/Libraries/LibMain",
    "//Userland/Libraries/LibRequests",
    "//Userland/Libraries/LibURL",
    "//Userland/Libraries/LibWeb",
    "//Userland/Libraries/LibWebSocket",
    "//Userland/Libraries/LibWebView",
    "//Userland/Libraries/LibWebView:WebContentClientEndpoint",
    "//Userland/Libraries/LibWebView:WebContentServerEndpoint",
    "//Userland/Libraries/LibWebView:WebDriverClientEndpoint",
    "//Userland/Libraries/LibWebView:WebDriverServerEndpoint",
  ]
  sources = [
    "//CryFox/FontPlugin.cpp",
    "//CryFox/HelperProcess.cpp",
    "//CryFox/ImageCodecPlugin.cpp",
    "//CryFox/Utilities.cpp",
    "//Userland/Services/WebContent/BackingStoreManager.cpp",
    "//Userland/Services/WebContent/ConnectionFromClient.cpp",
    "//Userland/Services/WebContent/ConsoleGlobalEnvironmentExtensions.cpp",
    "//Userland/Services/WebContent/PageClient.cpp",
    "//Userland/Services/WebContent/PageHost.cpp",
    "//Userland/Services/WebContent/WebContentConsoleClient.cpp",
    "//Userland/Services/WebContent/WebDriverConnection.cpp",
    "main.cpp",
  ]

  if (enable_qt) {
    defines = [ "HAVE_QT" ]
    configs += [ ":WebContent_qt" ]
    sources += [
      "//CryFox/Qt/EventLoopImplementationQt.cpp",
      "//CryFox/Qt/EventLoopImplementationQtEventTarget.cpp",
      "//CryFox/Qt/StringUtils.cpp",
    ]

    sources += get_target_outputs(":generate_moc")
    deps += [ ":generate_moc" ]
  }

  output_dir = "$root_out_dir/libexec"
}
